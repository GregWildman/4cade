;license:MIT
;(c) 2025 by qkumba/Frank M.

!cpu 6502
!to "build/PRELAUNCH.INDEXED/DG",plain
*=$106

    !source "src/macros.a"

         +ENABLE_ACCEL_AND_HIDE_ARTWORK
         inc   $3F4       ; force reboot
         ldy   #(callback_e - callback_b) - 1
-        lda   callback_b, y
         sta   callback, y
         dey
         bpl   -
         lda   #<callback
         sta   $8B2
         lda   #>callback
         sta   $8B3
         jsr   $800       ; load "DOS"
         lda   $c083
         jsr   EnableAccelerator
         jsr   $6083
         lda   $c083
         jsr   DisableAccelerator
         jmp   $6025

callback_b !pseudopc $380 {
callback
         lda   #$60
         sta   $6022
         sta   $c008
         lda   $c083
         jsr   DisableAccelerator
         lda   $c08b
         sta   $c009
         jmp   $6000
}
callback_e

!if * > PrelaunchMax {
  !error "code is too large, ends at ", *
}
