;license:MIT
;(c) 2019-2020 by 4am, qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/PITFALL.II",plain
*=$106

         !source "src/macros.a"

         +ENABLE_ACCEL_AND_HIDE_ARTWORK
         lda   #$60
         sta   $3D34
         jsr   $0800      ; decompress
         +RESET_VECTOR reset
         lda   #$4C
         sta   $2DF5
         sta   $2E06
         lda   #<callback1
         sta   $2DF6
         lda   #>callback1
         sta   $2DF7
         lda   #<callback2
         sta   $2E07
         lda   #>callback2
         sta   $2E08
         +READ_RAM2_NO_WRITE
         lda   MachineStatus
         and   #CHEATS_ENABLED
         beq   +
         lda   #$60
         sta   $243D      ; no collisions
+
         jsr   DisableAccelerator
         +READ_ROM_NO_WRITE
         jmp   $6000      ;612A copies pages 2E & 2F to AE & AF for final location

reset
         +READ_RAM2_NO_WRITE
         jmp   ($FFFC)

callback1
         sec
         sbc   #8
         cmp   #2
         bcc   +
-        jmp   $AE0A
+        jmp   $ADF9

callback2
         sec
         sbc   #8
         cmp   #2
         bcs   -
         jmp   $AE21

!if * > HideLaunchArtwork {
  !error "code is too large, ends at ", *
}
