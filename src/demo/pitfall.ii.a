;license:MIT
;(c) 2021-2022 by Frank M., qkumba

!cpu 6502
!to "res/DEMO/PITFALL.II",plain
*=$300

         !source "src/constants.a"   ; no code in these
         !source "src/macros.a"

         +GAME_REQUIRES_JOYSTICK
         +ENABLE_ACCEL_LC
         +LOAD_XSINGLE title

         lda   #$60
         sta   $3D34
         jsr   $0800      ; decompress

         +RESET_VECTOR reset
         ldx   #3-1
-        lda   patch,x
         sta   $2DF5,x
         lda   patch2,x
         sta   $2E06,x
         lda   patch3,x
         sta   $6015,x
         dex
         bpl   -

         +DISABLE_ACCEL

         jmp   $6000      ;612A copies pages 2E & 2F to AE & AF for final location

patch    !byte $4C,<callback1,>callback1
patch2   !byte $4C,<callback2,>callback2
patch3   !byte $4C,<callback3,>callback3

reset
         +READ_RAM2_NO_WRITE
         jmp   ($FFFC)

callback1
         sec
         sbc   #8
         cmp   #2
         bcc   +
-        jmp   $AE0A
+        jmp   $ADF9

callback2
         sec
         sbc   #8
         cmp   #2
         bcs   -
         jmp   $AE21

callback3
         ldx   #6-1
p1       lda   $100, x
         sta   $6055, x
         sta   $AB43, x
         dex
         bpl   p1         ; set demo exits

         lda   #$BF
         sta   $6021      ; reduce time before demo starts

         lda   #$D0
         sta   $6299      ; don't lose keypress on return
         jmp   $ADE0

title    !byte x_e-x_b
x_b      !text "PITFALL.II"
x_e

!if * > $3F0 {
  !error "code is too large, ends at ", *
}
